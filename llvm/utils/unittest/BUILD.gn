# Used to push the gtest include directories to things depending on :googletest.
config("googletest_config") {
  include_dirs = [
    "googlemock/include",
    "googletest/include",
  ]
}

static_library("googletest") {
  # LLVM requires C++11 but gtest doesn't correctly detect the availability
  # of C++11 on MSVC, so we force it on.
  defines = [
    "GTEST_LANG_CXX",
    "GTEST_HAS_TR1_TUPLE=0",
  ]
  if (host_os == "win") {
    defines += [ "GTEST_OS_WINDOWS" ]
  }
  defines += [ "GTEST_HAS_RTTI=0" ]
  deps = [
    "//llvm/lib/Support",
  ]
  include_dirs = [
    "googletest",  # For including src/gtest.cc
    "googlemock",  # For including src/gmock.cc
  ]
  public_configs = [ ":googletest_config" ]

  # FIXME: if (!llvm_enable_threads) GTEST_HAS_PTHREAD=0
  sources = [
    "googlemock/src/gmock-all.cc",
    "googletest/src/gtest-all.cc",
  ]
  testonly = true
}
