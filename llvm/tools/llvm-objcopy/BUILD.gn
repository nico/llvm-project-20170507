import("//gn/symlink.gni")
import("//llvm/utils/TableGen/tablegen.gni")

tablegen("ObjcopyOpts") {
  visibility = [ ":llvm-objcopy" ]
  args = [ "-gen-opt-parser-defs" ]
}

tablegen("StripOpts") {
  visibility = [ ":llvm-objcopy" ]
  args = [ "-gen-opt-parser-defs" ]
}

symlink_or_copy("llvm-strip") {
  deps = [
    ":llvm-objcopy",
  ]
  source = "llvm-objcopy"
  output = "$root_out_dir/bin/llvm-strip"
}

# //:llvm-objcopy depends on this symlink target, see comment in //BUILD.gn.
group("symlinks") {
  deps = [
    ":llvm-strip",
  ]
}

# FIXME: if llvm_install_binutils_symlinks, then symlink("objcopy")

executable("llvm-objcopy") {
  deps = [
    ":ObjcopyOpts",
    ":StripOpts",
    "//llvm/lib/MC",
    "//llvm/lib/Object",
    "//llvm/lib/Option",
    "//llvm/lib/Support",
  ]
  sources = [
    "Object.cpp",
    "llvm-objcopy.cpp",
  ]
}
