import("//llvm/include/llvm/Config/config.gni")

static_library("Support") {
  output_name = "LLVMSupport"
  deps = [
    "//gn:pthread",
    "//llvm/include/llvm/Config:config",
    "//llvm/lib/Demangle",
  ]
  public_deps = [
    # abi-breaking.h is also include by public headers in ADT, but ADT has
    # no target in the gn build (yet?).  Since everything depends on Support,
    # this public_dep does double duty of abi-breaking.h uses in public headers
    # of both Support and ADT.
    "//llvm/include/llvm/Config:abi-breaking",
    "//llvm/include/llvm/Config:llvm-config",
  ]
  include_dirs = [
    "Unix",
    "Windows",
  ]
  sources = [
    "AMDGPUMetadata.cpp",
    "APFloat.cpp",
    "APInt.cpp",
    "APSInt.cpp",
    "ARMAttributeParser.cpp",
    "ARMBuildAttrs.cpp",
    "ARMWinEH.cpp",
    "Allocator.cpp",
    "BinaryStreamError.cpp",
    "BinaryStreamReader.cpp",
    "BinaryStreamRef.cpp",
    "BinaryStreamWriter.cpp",
    "BlockFrequency.cpp",
    "BranchProbability.cpp",
    "COM.cpp",
    "CachePruning.cpp",
    "Chrono.cpp",
    "CodeGenCoverage.cpp",
    "CommandLine.cpp",
    "Compression.cpp",
    "ConvertUTF.cpp",
    "ConvertUTFWrapper.cpp",
    "CrashRecoveryContext.cpp",
    "DAGDeltaAlgorithm.cpp",
    "DJB.cpp",
    "DataExtractor.cpp",
    "Debug.cpp",
    "DebugCounter.cpp",
    "DeltaAlgorithm.cpp",
    "Error.cpp",
    "ErrorHandling.cpp",
    "FileOutputBuffer.cpp",
    "FileUtilities.cpp",
    "FoldingSet.cpp",
    "FormatVariadic.cpp",
    "FormattedStream.cpp",
    "GlobPattern.cpp",
    "GraphWriter.cpp",
    "Hashing.cpp",
    "InitLLVM.cpp",
    "IntEqClasses.cpp",
    "IntervalMap.cpp",
    "JamCRC.cpp",
    "KnownBits.cpp",
    "LEB128.cpp",
    "LineIterator.cpp",
    "Locale.cpp",
    "LockFileManager.cpp",
    "LowLevelType.cpp",
    "MD5.cpp",
    "ManagedStatic.cpp",
    "MathExtras.cpp",
    "MemoryBuffer.cpp",
    "NativeFormatting.cpp",
    "Options.cpp",
    "Parallel.cpp",
    "PluginLoader.cpp",
    "PrettyStackTrace.cpp",
    "RandomNumberGenerator.cpp",
    "Regex.cpp",
    "SHA1.cpp",
    "ScaledNumber.cpp",
    "ScopedPrinter.cpp",
    "SmallPtrSet.cpp",
    "SmallVector.cpp",
    "SourceMgr.cpp",
    "SpecialCaseList.cpp",
    "Statistic.cpp",
    "StringExtras.cpp",
    "StringMap.cpp",
    "StringPool.cpp",
    "StringRef.cpp",
    "StringSaver.cpp",
    "SystemUtils.cpp",
    "TarWriter.cpp",
    "TargetParser.cpp",
    "ThreadPool.cpp",
    "Timer.cpp",
    "ToolOutputFile.cpp",
    "TrigramIndex.cpp",
    "Triple.cpp",
    "Twine.cpp",
    "Unicode.cpp",
    "UnicodeCaseFold.cpp",
    "VersionTuple.cpp",
    "WithColor.cpp",
    "YAMLParser.cpp",
    "YAMLTraits.cpp",
    "circular_raw_ostream.cpp",
    "raw_os_ostream.cpp",
    "raw_ostream.cpp",
    "regcomp.c",
    "regerror.c",
    "regexec.c",
    "regfree.c",
    "regstrlcpy.c",
    "xxhash.cpp",

    # System
    "Atomic.cpp",
    "DynamicLibrary.cpp",
    "Errno.cpp",
    "Host.cpp",
    "Memory.cpp",
    "Mutex.cpp",
    "Path.cpp",
    "Process.cpp",
    "Program.cpp",
    "RWMutex.cpp",
    "Signals.cpp",
    "TargetRegistry.cpp",
    "ThreadLocal.cpp",
    "Threading.cpp",
    "Valgrind.cpp",
    "Watchdog.cpp",
  ]

  libs = []

  if (host_os == "linux") {
    libs += [ "dl" ]
  }

  if (llvm_enable_terminfo) {
    libs += [ "ncurses" ]
  }

  if (llvm_enable_zlib) {
    libs += [ "z" ]
  }
}
