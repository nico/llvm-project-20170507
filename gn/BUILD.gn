declare_args() {
  # Whether to build with debug information and without optimizations.
  is_debug = true
}

declare_args() {
  # Whether to enable assertions.
  llvm_enable_assertions = is_debug
}

config("compiler_defaults") {
  # FIXME: Don't define this globally here.
  defines = [ "LLVM_ON_UNIX" ]
  cflags = [
    # FIXME: only pass this on mac, and probably only if clang_base_path is set.
    "-isysroot",
    "/Applications/Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX.sdk",
  ]

  if (is_debug) {
    cflags += [ "-g" ]
  } else {
    cflags += [ "-O3" ]
  }

  if (!llvm_enable_assertions) {
    defines += [ "NDEBUG" ]
  }

  cflags_cc = [ "-std=c++11" ]
}

config("llvm_code") {
  include_dirs = [
    "//llvm/include",
    "$root_gen_dir/llvm/include",
  ]
}

config("lld_code") {
  include_dirs = [
    "//lld/include",
    "$root_gen_dir/lld/include",
  ]
}

config("clang_code") {
  include_dirs = [
    "//clang/include",
    "$root_gen_dir/clang/include",
  ]
}
