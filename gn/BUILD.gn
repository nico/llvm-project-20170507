import("//gn/buildflags.gni")
import("//gn/mac_sdk.gni")

declare_args() {
  # Whether to enable threading.
  llvm_enable_threads = true
}

# args that depend on other args must live in a later declare_args() block.
declare_args() {
  # Whether to enable assertions.
  llvm_enable_assertions = is_debug
}

config("compiler_defaults") {
  # FIXME: Don't define this globally here.
  defines = [ "LLVM_ON_UNIX" ]
  cflags = []

  if (host_os == "mac") {
    cflags += [
      # FIXME: probably only do this if clang_base_path is set.
      "-isysroot",
      mac_sdk,
    ]
  }

  if (is_debug) {
    cflags += [ "-g" ]
  } else {
    cflags += [ "-O3" ]
  }

  if (!llvm_enable_assertions) {
    defines += [ "NDEBUG" ]
  }

  cflags_cc = [ "-std=c++11" ]
}

config("llvm_code") {
  include_dirs = [
    "//llvm/include",
    "$root_gen_dir/llvm/include",
  ]
}

config("lld_code") {
  include_dirs = [
    "//lld/include",
    "$root_gen_dir/lld/include",
  ]
}

config("clang_code") {
  include_dirs = [
    "//clang/include",
    "$root_gen_dir/clang/include",
  ]
}

config("pthread_config") {
  visibility = [ ":pthread" ]
  libs = [ "pthread" ]

  # FIXME: currently must match config.h
  defines = [ "LLVM_ENABLE_THREADS" ]
}

group("pthread") {
  if (llvm_enable_threads) {
    public_configs = [ ":pthread_config" ]
  }
}
