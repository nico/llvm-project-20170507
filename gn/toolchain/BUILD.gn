declare_args() {
  # Whether to use goma (https://chromium.googlesource.com/infra/goma/client/)
  use_goma = false

  # If is_goma is true, the location of the goma client install.
  # FIXME: make default to C:\src\goma\goma-win64 on Windows
  goma_dir = getenv("HOME") + "/goma"

  # Set this to a clang build directory. If set, that clang is used as compiler.
  # goma only works with compiler binaries it knows about, so useful both for
  # using a goma-approved compiler and for compiling clang with a locally-built
  # clang in a different build directory.
  clang_base_path = ""
}

toolchain("cc") {
  cc = "cc"
  cxx = "c++"

  if (clang_base_path != "") {
    cc = "$clang_base_path/bin/clang"
    cxx = "$clang_base_path/bin/clang++"
  }

  ld = cxx  # Don't use goma wrapper for linking.
  if (use_goma) {
    cc = "$goma_dir/gomacc $cc"
    cxx = "$goma_dir/gomacc $cxx"
  }

  tool("cc") {
    depfile = "{{output}}.d"
    command = "$cc -MMD -MF $depfile -o {{output}} -c {{source}} {{defines}} {{include_dirs}} {{cflags}} {{cflags_c}}"
    depsformat = "gcc"
    description = "CC {{output}}"
    outputs = [
      "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o",
    ]
  }

  tool("cxx") {
    depfile = "{{output}}.d"
    command = "$cxx -MMD -MF $depfile -o {{output}} -c {{source}} {{defines}} {{include_dirs}} {{cflags}} {{cflags_cc}}"
    depsformat = "gcc"
    description = "CXX {{output}}"
    outputs = [
      "{{source_out_dir}}/{{target_output_name}}.{{source_name_part}}.o",
    ]
  }

  tool("alink") {
    if (host_os == "mac") {
      command = "libtool -static {{arflags}} -o {{output}} {{inputs}}"
    } else {
      command = "ar rcsDT {{arflags}} -o {{output}} {{inputs}}"
    }
    description = "LIBTOOL-STATIC {{output}}"
    outputs = [
      "{{target_out_dir}}/{{target_output_name}}{{output_extension}}",
    ]
    default_output_extension = ".a"
    output_prefix = "lib"
  }

  tool("link") {
    outfile = "{{output_dir}}/{{target_output_name}}{{output_extension}}"
    if (host_os == "mac") {
      command = "$ld {{ldflags}} -o $outfile {{libs}} {{inputs}}"
    } else {
      command = "$ld {{ldflags}} -o $outfile {{libs}} -Wl,--start-group {{inputs}} -Wl,--end-group"
    }
    description = "LINK $outfile"
    outputs = [
      outfile,
    ]
    lib_switch = "-l"

    # Setting this allows targets to override the default executable output by
    # setting output_dir.
    # FIXME: default to {{root_out_dir}}/bin? (need to change all scripts
    # referring to build-time binaries then -- llvm-tblgen, clang-tblgen)
    default_output_dir = "{{root_out_dir}}"
  }

  tool("copy") {
    command = "ln -f {{source}} {{output}} 2>/dev/null || (rm -rf {{output}} && cp -af {{source}} {{output}})"
    description = "COPY {{source}} {{output}}"
  }

  tool("stamp") {
    command = "touch {{output}}"
    description = "STAMP {{output}}"
  }
}
