import("//gn/symlink.gni")

symlinks = [
  # FIXME: gn embeds the + into a ninja rule name, but ninja doesn't accept +
  # in rule names.
  #"clang++",
  "clang-cl",
  "clang-cpp",
]
foreach(target, symlinks) {
  symlink(target) {
    source = "clang"
    output = "$root_out_dir/$target"
  }
}

executable("clang") {
  configs += [ "//gn:clang_code" ]
  deps = [
    "//clang/lib/Basic",
    "//clang/lib/CodeGen",
    "//clang/lib/Driver",
    "//clang/lib/Frontend",
    "//clang/lib/FrontendTool",
    "//llvm/lib/Analysis",
    "//llvm/lib/CodeGen",
    "//llvm/lib/IR",
    "//llvm/lib/MC",
    "//llvm/lib/MC/MCParser",
    "//llvm/lib/Option",
    "//llvm/lib/Support",
    "//llvm/lib/Target:TargetsToBuild",
    "//llvm/lib/Transforms/IPO",
    "//llvm/lib/Transforms/InstCombine",
    "//llvm/lib/Transforms/Instrumentation",
    "//llvm/lib/Transforms/ObjCARC",
    "//llvm/lib/Transforms/Scalar",
    "//llvm/lib/Transforms/Utils",
    "//llvm/lib/Transforms/Vectorize",
  ]
  sources = [
    "cc1_main.cpp",
    "cc1as_main.cpp",
    "driver.cpp",
  ]

  # FIXME: Info.plist embedding for mac builds.
}
