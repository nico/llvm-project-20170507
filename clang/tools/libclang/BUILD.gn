import("//clang/lib/ARCMigrate/enable.gni")
import("//llvm/version.gni")

libclang_target_type = "shared_library"
if (host_os == "linux") {
  # Linux needs -fPIC to build shared libs but they aren't on by default.
  # For now, make libclang a static lib there.
  libclang_target_type = "static_library"
}

target(libclang_target_type, "libclang") {
  configs += [ "//gn:clang_code" ]
  deps = [
    "//clang/lib/AST",
    "//clang/lib/Basic",
    "//clang/lib/Frontend",
    "//clang/lib/Headers",
    "//clang/lib/Index",
    "//clang/lib/Lex",
    "//clang/lib/Sema",
    "//clang/lib/Tooling",
    "//llvm/lib/IR",
    "//llvm/lib/Support",
    "//llvm/lib/Target:TargetsToBuild",
  ]
  if (clang_enable_arcmt) {
    deps += [ "//clang/lib/ARCMigrate" ]
  }

  # FIXME: clangTidyPlugin
  # FIXME: libclang_build_static?
  # FIXME: libclang.exports
  # FIXME: llvm_enable_pic
  sources = [
    "ARCMigrate.cpp",
    "BuildSystem.cpp",
    "CIndex.cpp",
    "CIndexCXX.cpp",
    "CIndexCodeCompletion.cpp",
    "CIndexDiagnostic.cpp",
    "CIndexHigh.cpp",
    "CIndexInclusionStack.cpp",
    "CIndexUSRs.cpp",
    "CIndexer.cpp",
    "CXComment.cpp",
    "CXCompilationDatabase.cpp",
    "CXCursor.cpp",
    "CXIndexDataConsumer.cpp",
    "CXLoadedDiagnostic.cpp",
    "CXSourceLocation.cpp",
    "CXStoredDiagnostic.cpp",
    "CXString.cpp",
    "CXType.cpp",
    "Indexing.cpp",
  ]
  if (host_os == "mac") {
    ldflags = [
      "-Wl,-compatibility_version,1",
      "-Wl,-current_version,$llvm_version",
      "-Wl,-install_name,@rpath/libclang.dylib",
    ]
  }
}
